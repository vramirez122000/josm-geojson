plugins {
  id "de.undercouch.download" version "2.0.0"
}

apply from: 'dependencies.gradle'
apply plugin: 'java'
apply plugin: 'checkstyle'

checkstyle {
    toolVersion = "6.0"
}

group = 'org.openstreetmap.josm.plugins'
version = '0.0.0-SNAPSHOT'

// Change JOSM version (if needed) before you build
project.ext.josm_version='9060'
project.ext.josm_location='download'
//project.ext.josm_location='download/Archiv'

sourceCompatibility=1.7
targetCompatibility=1.7

repositories
{ mavenCentral() }

dependencies
{
    // Josm is not on Maven Central
    compile files("lib/josm-snapshot-${josm_version}.jar")
    compile packages.geojsonjackson
    compile packages.jackson.core
    compile packages.jackson.databind
    compile packages.jackson.annotations

    testCompile packages.junit
}

jar
{
    from{
        configurations.compile.findAll
        {
            it.name != "josm-snapshot-${josm_version}.jar"
        }.collect
        {
            it.isDirectory() ? it : zipTree(it).matching{
                exclude
                {
                    it.path.contains('META-INF') && (it.path.endsWith('.SF') || it.path.endsWith('.DSA') || it.path.endsWith('.RSA'))
                }
            }
        }
    }
    manifest
    {
        attributes("Author": "Ian Dees and matthieun and Victor Ramirez",
        "Plugin-Class": "org.openstreetmap.josm.plugins.geojson.GeoJsonPlugin",
        "Plugin-Date": "2015-06-07T23:12:29.582966Z",
        "Plugin-Description": "Allows you to view a GeoJSON file as a layer.",
        "Plugin-Icon": "images/preferences/geojson.png",
        "Plugin-Link": "http://wiki.openstreetmap.org/index.php/JOSM/Plugins/GeoJSON",
        "Plugin-Mainversion": "7777",
        "Plugin-Version": "31241",
        "Plugin-Canloadatruntime": "true",
        )
    }
}

import de.undercouch.gradle.tasks.download.Download

task josm(type: Download) {
    def folder = new File( 'lib' )
    if( !folder.exists() ) {
        folder.mkdirs()
    }
    src "http://josm.openstreetmap.de/${josm_location}/josm-snapshot-${josm_version}.jar"
    dest folder
}

import org.gradle.internal.os.OperatingSystem

task installPlugin(type: Copy, dependsOn: jar) {
    def destStr
    if(OperatingSystem.current().isMacOsX()) {
        destStr = "${System.getProperty('user.home')}/Library/JOSM/plugins"
    } else if(OperatingSystem.current().isWindows()) {
        destStr = "${System.getenv()['APPDATA']}/JOSM/plugins"
    } else {
        destStr = "${System.getProperty('user.home')}/.josm/plugins"
    }
    println(jar.archivePath)
    from jar.archivePath
    into destStr
    rename {
        'geojson.jar'
    }
}
